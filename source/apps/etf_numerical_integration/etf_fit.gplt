# Physical constants
alpha = 7.2973525664e-3
hbarc = 197.3269788
rmn = 939.5654133

# Empirical parameters
rho0 = 0.154
lasat0 = -16.04
ksat0 = 255.91
jsym0 = 33.43
lsym0 = 77.92
ksym0 = -2.19
effm = 0.7
qq = 144.5*jsym0/(lsym0+55.5)

# Coulomb energy
aC = 3.*alpha*hbarc/5.*(4.*pi/3.)**(1./3.)*rho0**(1./3.)

# f(x,y) = 1.-2.*y/x # test
f(x,y) = (1.-2.*y/x + 3.*aC/8./qq*y*y/x**(5.3))/(1.+9.*jsym0/4./qq/x**(1./3.))
rhosat(x,y)=rho0*(1.-3.*lsym0*f(x,y)*f(x,y)/(ksat0+ksym0*f(x,y)*f(x,y)))
rsat(x,y) = (4.*pi*rhosat(x,y)/3.)**(-1./3.)

# Bulk energy
barm = 1./effm - 1.
t0fg = 3.*hbarc*hbarc/10./rmn*(3.*pi*pi*rho0/2.)**(2./3.)
e00 = lasat0 - t0fg*(1.+barm)
e01 = -t0fg*(2.+5.*barm)
e02 = ksat0 - 2.*t0fg*(5.*barm-1.)
e20 = jsym0 - 5./9.*t0fg*(1.+barm)
e21 = lsym0 - 5./9.*t0fg*(2.+5.*barm)
e22 = ksym0 -10./9.*t0fg*(-1.+5.*barm)
g(x,y) = -lsym0*f(x,y)*f(x,y)/(ksat0+ksym0*f(x,y)*f(x,y))
u0(x,y) = 1.-(-3*g(x,y))**(3.)*exp(-10.*log(2)*(1.+3.*g(x,y)))
u1(x,y) = 1.-(-3*g(x,y))**(2.)*exp(-10.*log(2)*(1.+3.*g(x,y)))
u2(x,y) = 1.-(-3*g(x,y))**(1.)*exp(-10.*log(2)*(1.+3.*g(x,y)))
bulk_energy(x,y) = 0.5*t0fg*(1.+3.*g(x,y))**(2./3.)*(1.+barm*(1.+3.*g(x,y)))*((1.+f(x,y))**(5./3.)+(1.-f(x,y))**(5./3.)) + e00*u0(x,y) + e01*u1(x,y)*g(x,y) + 0.5*u2(x,y)*g(x,y)*g(x,y) + f(x,y)*f(x,y)*(e20*u0(x,y) + e21*u1(x,y)*g(x,y) + 0.5*e22*u2(x,y)*g(x,y)*g(x,y))

# Fit
h(x,y)=(4.*pi*rsat(x,y)*rsat(x,y)*a+b*f(x,y)*f(x,y))*x**(-1./3.)
FIT_LIMIT=1e-12
fit h(x,y) "./etf.table" using 1:2:3:(1) via a,b

# Plot
set title 'A=200'
set xlabel 'I'
set ylabel '(E_{ETF,para}-E_{ETF})/A [MeV]'
aa = 200.
zz(x) = aa*(1.-x)/2.
e(x) = bulk_energy(aa,zz(x)) + (4.*pi*rsat(aa,zz(x))*rsat(aa,zz(x))*a+b*f(aa,zz(x))*f(aa,zz(x)))*aa**(-1./3.)
plot [-0.6:0.6] e(x) lw 4 t 'parabolic ETF', 'aa200bis.out' u 1:2 w l lw 4 t 'ETF'

pause -1
